?start: visualization

?visualization: datasets charts

datasets: "DATASETS" (CNAME ESCAPED_STRING)+

charts: chart+

vcat: "(" chart+ ")"

hcat: "CONCAT" "(" chart+ ")"

chart: xy_chart | histogram_chart | pie_chart | vcat | hcat

xy_chart: markxy data title? dimensions filter?

histogram_chart: "HISTOGRAM" data title? field histogram_opts? filter?

pie_chart: "PIE" data title? field pie_opts? filter?

title: "TITLE" ESCAPED_STRING

filter: "FILTER" ESCAPED_STRING

markxy: SCATTER | LINE | BAR

data: CNAME

dimensions: (x | y | split_by)+

histogram_opts: (step | bins) label?

pie_opts: hole

step: "STEP" NUMBER

bins: "BINS" NUMBER

hole: "HOLE" NUMBER

// This allows for both x and y to have aggregations, which results in
// order ambiguity (i.e. which aggregation gets applied first?).
// TODO: Find a way to enforce only one aggregation transformation
// between x and y.
x: "X" ((aggregation field) | (field temporal?)) label?

y: "Y" ((aggregation field) | (field temporal?)) label?

label: "LABEL" ESCAPED_STRING

// For split by temporal is supported irrespective of x and y.
split_by: "SPLIT" "BY" field? temporal?

field: CNAME

aggregation: COUNT | MIN | MAX | AVG

temporal: "BY" (YEAR | MONTH | DAY | HOUR | MINUTE | SECOND)

SCATTER: "SCATTER"

BAR: "BAR"

LINE: "LINE"

HISTOGRAM: "HISTOGRAM"

PIE: "PIE"

COUNT: "COUNT"

MIN: "MIN"

MAX: "MAX"

AVG: "AVG"

YEAR: "YEAR"

MONTH: "MONTH"

DAY: "DAY"

HOUR: "HOUR"

MINUTE: "MINUTE"

SECOND: "SECOND"

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.WS

%ignore WS