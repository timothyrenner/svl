?start: visualization

?visualization: datasets charts

datasets: "DATASETS" (CNAME ESCAPED_STRING)+

charts: chart+

vcat: "(" chart+ ")"

chart: (mark2d data dimensions) | (mark1d data mark1d_opts?) | vcat

mark2d: SCATTER | LINE | BAR | BOXPLOT

data: CNAME

dimensions: (x | y | color)+

mark1d: HISTOGRAM

mark1d_opts: field (step | bins)

step: "STEP" NUMBER

bins: "BINS" NUMBER

x: "X" field? (aggregation? | temporal?)

y: "Y" field? (aggregation? | temporal?)

color: "COLOR" field? (aggregation? | temporal?)

field: CNAME

aggregation: COUNT | MIN | MAX | AVG

temporal: "BY" (YEAR | QUARTER | MONTH | DAY | HOUR | MINUTE | SECOND)

SCATTER: "SCATTER"

BAR: "BAR"

LINE: "LINE"

HISTOGRAM: "HISTOGRAM"

BOXPLOT: "BOXPLOT"

COUNT: "COUNT"

MIN: "MIN"

MAX: "MAX"

AVG: "AVG"

YEAR: "YEAR"

QUARTER: "QUARTER"

MONTH: "MONTH"

DAY: "DAY"

HOUR: "HOUR"

MINUTE: "MINUTE"

SECOND: "SECOND"

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.WS

%ignore WS