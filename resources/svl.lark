?start: visualization

?visualization: datasets charts

datasets: "DATASETS" (CNAME ESCAPED_STRING)+

charts: chart+

vcat: "(" chart+ ")"

hcat: "CONCAT" "(" chart+ ")"

chart: (mark2d data dimensions) | (mark1d data mark1d_opts?) | vcat | hcat

mark2d: SCATTER | LINE | BAR | BOXPLOT

data: CNAME

// TODO: I don't think color should take aggs, but it can be bucketed.
// X and Y should be bucketable too.
dimensions: (x | y | color)+

mark1d: HISTOGRAM

mark1d_opts: field (step | bins)

step: "STEP" NUMBER

bins: "BINS" NUMBER

// This allows for both x and y to have aggregations, which results in
// order ambiguity (i.e. which aggregation gets applied first?).
// TODO: Find a way to enforce only one aggregation transformation.
// x: "X" field? (aggregation? | temporal?)
x: "X" ((aggregation field) | (field temporal?))

// y: "Y" field? (aggregation? | temporal?)
y: "Y" ((aggregation field) | (field temporal?))

// For color temporal is supported irrespective of x and y.
// TODO: Color should not take aggs.
color: "COLOR" field? (aggregation? | temporal?)

field: CNAME

aggregation: COUNT | MIN | MAX | AVG

temporal: "BY" (YEAR | MONTH | DAY | HOUR | MINUTE | SECOND)

SCATTER: "SCATTER"

BAR: "BAR"

LINE: "LINE"

HISTOGRAM: "HISTOGRAM"

BOXPLOT: "BOXPLOT"

COUNT: "COUNT"

MIN: "MIN"

MAX: "MAX"

AVG: "AVG"

YEAR: "YEAR"

MONTH: "MONTH"

DAY: "DAY"

HOUR: "HOUR"

MINUTE: "MINUTE"

SECOND: "SECOND"

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.WS

%ignore WS