DATASETS 
    bigfoot "data/bigfoot_sightings.csv"

LINE bigfoot
    TITLE "Bigfoot Sightings by Year"
    X date BY YEAR LABEL "Year of Sighting"
    Y date COUNT LABEL "Number of Sightings"
    SPLIT BY classification
    FILTER "date > '1960-01-01'"

CONCAT(
    (
        BAR bigfoot
            TITLE "Bigfoot Sightings by Classification"
            X classification LABEL "Classification"
            Y temperature_mid COUNT LABEL "Number of Sightings"
        PIE bigfoot
            TITLE "Bigfoot Sightings With Location"
            HOLE 0.3
            AXIS TRANSFORM "CASE WHEN latitude IS NULL THEN 'no_location' ELSE 'has_location' END"
    )
    
    (
        CONCAT(
            HISTOGRAM bigfoot
                TITLE "Bigfoot Sighting High Temperature"
                AXIS temperature_high LABEL "Temperature (F)"
                BINS 25

            HISTOGRAM bigfoot
                BINS 25
                AXIS temperature_low LABEL "Temperature (F)"
                TITLE "Bigfoot Sighting Low Temperature"
        )
    
        HISTOGRAM bigfoot
            BINS 25
            TITLE "Bigfoot Sighting Mid Temperature"
            AXIS temperature_mid LABEL "Temperature (F)"
        

    )
)

SCATTER bigfoot
    TITLE "Bigfoot Sighting Moon Phase by Latitude"
    X latitude LABEL "Latitude (deg)"
    Y moon_phase LABEL "Moon Phase"